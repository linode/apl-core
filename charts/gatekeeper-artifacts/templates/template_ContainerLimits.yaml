apiVersion: templates.gatekeeper.sh/v1beta1
kind: ConstraintTemplate
metadata:
  creationTimestamp: null
  name: containerlimits
spec:
  crd:
    spec:
      names:
        kind: ContainerLimits
  targets:
  - libs:
    - |-
      package lib.core

      default is_gatekeeper = false

      is_gatekeeper {
        has_field(input, "review")
        has_field(input.review, "object")
      }

      resource = input.review.object {
        is_gatekeeper
      }

      resource = input {
        not is_gatekeeper
      }

      review = input.review {
        is_gatekeeper
      }

      review = {"object": resource, "kind": {"group": group, "kind": kind, "version": version}} {
        not is_gatekeeper
      }

      parameters = input.parameters {
        trace(sprintf("has input.parameters: %v", [input.parameters]))
        is_gatekeeper
      }

      format(msg) = {"msg": msg}

      format_with_id(msg, id) = msg_fmt {
        msg_fmt := {
          "msg": sprintf("%s: %s", [id, msg]),
          "details": {"policyID": id},
        }
      }

      apiVersion := resource.apiVersion

      name := resource.metadata.name

      gv := split(apiVersion, "/")

      group = gv[0] {
        contains(apiVersion, "/")
      }

      group = "core" {
        not contains(apiVersion, "/")
      }

      version := gv[minus(count(gv), 1)]

      kind := resource.kind

      labels := resource.metadata.labels

      annotations := resource.metadata.annotations

      has_field(obj, field) {
        not object.get(obj, field, "N_DEFINED") == "N_DEFINED"
      }

      missing_field(obj, field) {
        obj[field] == ""
      }

      missing_field(obj, field) {
        not has_field(obj, field)
      }
    - |-
      package lib.exceptions


      import data.lib.annotations
      import data.lib.core
      import data.lib.parameters

      get_safe_annotation[return] {
        all_annotations := annotations.merge_annotations()
        trace(sprintf("all_annotations: %v", [all_annotations]))
        policy_list := sprintf("%s,%s", [
          object.get(all_annotations, annotations.ignoreAnnotationField, ""),
        ])

        trace(sprintf("pod ignore list: %v", [policy_list]))
        return := split(policy_list, ",")
      }

      is_exception(policyID) {
        get_safe_annotation[ignore_list]
        ignore_list[_] == policyID
      }

      is_container_exception(cname, policyID) {
        all_annotations := annotations.merge_annotations()
        policy_list := object.get(all_annotations, annotations.get_container_ignore_field(cname), "")
        trace(sprintf("container ignore list: %v", [policy_list]))
        ignore_list := split(policy_list, ",")
        ignore_list[_] == policyID
      }

      is_exception(policyID) {
        not parameters.policy_parameters(policyID).enabled
      }
    - |-
      package lib.annotations


      import data.lib.core
      import data.lib.pods

      default ignoreAnnotationField = "policy.otomi.io/ignore"

      default paramsAnnotationField = "policy.otomi.io/parameters"

      default containerIgnoreAnnotationField = "policy.otomi.io/ignore.%s"

      default containerParamAnnotationField = "policy.otomi.io/parameters.%s.%s"

      get_container_params_field(cname, policyID) = return {
        return := sprintf(containerParamAnnotationField, [cname, policyID])
      }

      get_container_ignore_field(cname) = return {
        return := sprintf(containerIgnoreAnnotationField, [cname])
      }

      get_default(object, field, _default) = output {
        core.has_field(object, field)
        object[field] != null
        output := object[field]
      }

      get_default(object, field, _default) = output {
        core.has_field(object, field) == false
        output := _default
      }

      merge_annotations = return {
        return := object.union(get_default(core.resource.metadata, "annotations", {}), get_default(pods.pod.metadata, "annotations", {}))
      } else = return {
        return := get_default(pods.pod.metadata, "annotations", {})
        return != null
      } else = return {
        return := get_default(core.resource.metadata, "annotations", {})
        return != null
      } else = return {
        return := {}
      }
    - |-
      package lib.pods

      import data.lib.core

      default pod = false

      pod = core.resource.spec.template {
        pod_templates := ["daemonset", "deployment", "job", "pod", "replicaset", "replicationcontroller", "statefulset"]
        lower(core.kind) == pod_templates[_]
      }

      pod = core.resource.spec.template {
        lower(core.apiVersion) == "serving.knative.dev/v1"
        lower(core.kind) == "service"
      }

      pod = core.resource {
        lower(core.kind) == "pod"
      }

      pod = core.resource.spec.jobTemplate.spec.template {
        lower(core.kind) == "cronjob"
      }

      containers[container] {
        keys := {"containers", "initContainers"}
        all_containers := [c | keys[k]; c := pod.spec[k][_]]
        container := all_containers[_]
      }

      volumes[volume] {
        volume := pod.spec.volumes[_]
      }
    - |-
      package lib.parameters


      import data.lib.annotations
      import data.lib.core

      policy_parameters(policyID) = params {
        trace(sprintf("core_params: %v", [{c | c = core.parameters}]))
        core_params := {c | c = core.parameters[policyID]}

        # trace(sprintf("core_params[policyID]: %v", [core_params]))
        extra_params := {e | e = extra_parameters(policyID)}

        # trace(sprintf("extra_params: %v", [extra_params]))
        params := object.union(core.parameters[policyID], extra_parameters(policyID))
        # trace(sprintf("policy_parameters: %v", [params]))
      }

      extra_parameters(policyID) = params {
        all_annotations := annotations.merge_annotations()
        policyAnnotationField := sprintf("%s.%s", [annotations.paramsAnnotationField, policyID])
        core.has_field(all_annotations, policyAnnotationField)
        params := json.unmarshal(all_annotations[policyAnnotationField])
        trace(sprintf("extra_parameters: %v", [params]))
      } else = params {
        params := {}
      }
    rego: |-
      package containerlimits

      import data.lib.core
      import data.lib.exceptions
      import data.lib.parameters
      import data.lib.pods

      policyID = "container-limits"

      missing(obj, field) {
        not obj[field]
      }

      missing(obj, field) {
        obj[field] == ""
      }

      canonify_cpu(orig) = new {
        is_number(orig)
        new := orig * 1000
      }

      canonify_cpu(orig) = new {
        not is_number(orig)
        endswith(orig, "m")
        new := to_number(replace(orig, "m", ""))
      }

      canonify_cpu(orig) = new {
        not is_number(orig)
        not endswith(orig, "m")
        re_match("^[0-9]+(\\.[0-9]+)?$", orig)
        new := to_number(orig) * 1000
      }

      mem_multiple("E") = 1000000000000000000

      mem_multiple("P") = 1000000000000000

      mem_multiple("T") = 1000000000000

      mem_multiple("G") = 1000000000

      mem_multiple("M") = 1000000

      mem_multiple("K") = 1000

      mem_multiple("") = 1

      mem_multiple("Ki") = 1024

      mem_multiple("Mi") = 1048576

      mem_multiple("Gi") = 1073741824

      mem_multiple("Ti") = 1099511627776

      mem_multiple("Pi") = 1125899906842624

      mem_multiple("Ei") = 1152921504606846976

      get_suffix(mem) = suffix {
        not is_string(mem)
        suffix := ""
      }

      get_suffix(mem) = suffix {
        is_string(mem)
        count(mem) > 0
        suffix := substring(mem, count(mem) - 1, -1)
        mem_multiple(suffix)
      }

      get_suffix(mem) = suffix {
        is_string(mem)
        count(mem) > 1
        suffix := substring(mem, count(mem) - 2, -1)
        mem_multiple(suffix)
      }

      get_suffix(mem) = suffix {
        is_string(mem)
        count(mem) > 1
        not mem_multiple(substring(mem, count(mem) - 1, -1))
        not mem_multiple(substring(mem, count(mem) - 2, -1))
        suffix := ""
      }

      get_suffix(mem) = suffix {
        is_string(mem)
        count(mem) == 1
        not mem_multiple(substring(mem, count(mem) - 1, -1))
        suffix := ""
      }

      get_suffix(mem) = suffix {
        is_string(mem)
        count(mem) == 0
        suffix := ""
      }

      canonify_mem(orig) = new {
        is_number(orig)
        new := orig
      }

      canonify_mem(orig) = new {
        not is_number(orig)
        suffix := get_suffix(orig)
        raw := replace(orig, suffix, "")
        re_match("^[0-9]+(\\.[0-9]+)?$", raw)
        new := to_number(raw) * mem_multiple(suffix)
      }

      violation[{"msg": msg}] {
        not exceptions.is_exception(policyID)
        general_violation[{"msg": msg}]
      }

      general_violation[{"msg": msg}] {
        pods.containers[container]
        not exceptions.is_container_exception(container.name, policyID)
        cpu_orig := container.resources.limits.cpu
        not canonify_cpu(cpu_orig)
        msg := sprintf("Policy: %s - container <%v> cpu limit <%v> could not be parsed", [policyID, container.name, cpu_orig])
      }

      general_violation[{"msg": msg}] {
        pods.containers[container]
        not exceptions.is_container_exception(container.name, policyID)
        mem_orig := container.resources.limits.memory
        not canonify_mem(mem_orig)
        msg := sprintf("Policy: %s - container <%v> memory limit <%v> could not be parsed", [policyID, container.name, mem_orig])
      }

      general_violation[{"msg": msg}] {
        pods.containers[container]
        not exceptions.is_container_exception(container.name, policyID)
        not container.resources
        msg := sprintf("Policy: %s - container <%v> has no resource limits", [policyID, container.name])
      }

      general_violation[{"msg": msg}] {
        pods.containers[container]
        not exceptions.is_container_exception(container.name, policyID)
        not container.resources.limits
        msg := sprintf("Policy: %s - container <%v> has no resource limits", [policyID, container.name])
      }

      general_violation[{"msg": msg}] {
        pods.containers[container]
        not exceptions.is_container_exception(container.name, policyID)
        missing(container.resources.limits, "cpu")
        msg := sprintf("Policy: %s - container <%v> has no cpu limit", [policyID, container.name])
      }

      general_violation[{"msg": msg}] {
        pods.containers[container]
        not exceptions.is_container_exception(container.name, policyID)
        missing(container.resources.limits, "memory")
        msg := sprintf("Policy: %s - container <%v> has no memory limit", [policyID, container.name])
      }

      general_violation[{"msg": msg}] {
        pods.containers[container]
        not exceptions.is_container_exception(container.name, policyID)
        cpu_orig := container.resources.limits.cpu
        cpu := canonify_cpu(cpu_orig)
        max_cpu_orig := parameters.policy_parameters(policyID).cpu
        max_cpu := canonify_cpu(max_cpu_orig)
        cpu > max_cpu
        msg := sprintf("Policy: %s - container <%v> cpu limit <%v> is higher than the maximum allowed of <%v>", [policyID, container.name, cpu_orig, max_cpu_orig])
      }

      general_violation[{"msg": msg}] {
        pods.containers[container]
        not exceptions.is_container_exception(container.name, policyID)
        mem_orig := container.resources.limits.memory
        mem := canonify_mem(mem_orig)
        max_mem_orig := parameters.policy_parameters(policyID).memory
        max_mem := canonify_mem(max_mem_orig)
        mem > max_mem
        msg := sprintf("Policy: %s - container <%v> memory limit <%v> is higher than the maximum allowed of <%v>", [policyID, container.name, mem_orig, max_mem_orig])
      }
    target: admission.k8s.gatekeeper.sh
status: {}
